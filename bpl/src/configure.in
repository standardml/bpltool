dnl Process this file with autoconf to produce a configure script

AC_PREREQ(2.50)

AC_REVISION()

AC_INIT(README)

AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_INSTALL
AC_EXEEXT

AC_CHECK_PROGS(SED,gnused gsed sed)
AC_CHECK_PROGS(PERL,perl)
AC_CHECK_PROGS(MOSML,mosml)
AC_CHECK_PROGS(MOSMLC,mosmlc)
AC_CHECK_PROGS(MLYACC,mlyacc ml-yacc ml-yacc.bat)
AC_CHECK_PROGS(MLLEX,mllex ml-lex ml-lex.bat)
AC_CHECK_PROGS(SMLNJ,sml-cm sml sml.bat)
AC_CHECK_PROGS(MLTON,mlton)
AC_ARG_VAR(MLC,ML compiler)
if test -z "$MLC"
then
	if test -n "$ac_cv_prog_SMLNJ"
	then
		MLC="smlnj"
	elif test -n "$ac_cv_prog_MOSMLC"
	then
		MLC="mosml"
	elif test -n "$ac_cv_prog_MLTON"
	then
		MLC="mlton"
	fi
fi
srcdir=`cd $srcdir && pwd`

AC_CACHE_VAL([bpl_cv_with_smldocdir],[bpl_cv_with_smldocdir=\${datadir}/doc/bpl])
AC_ARG_WITH([smldocdir],
            AS_HELP_STRING([--with-smldocdir=DIR],
                           [directory for API documentation (\$(datadir)/doc/bpl)]),
            [bpl_cv_with_smldocdir="$withval"])
AC_SUBST([smldocdir],[$bpl_cv_with_smldocdir])

dnl  Provoke rebuilding of mosmake.dep each time the configuration
dnl  or the master Makefile changes; such a change may have affected
dnl  the set of directories known to Mosmake. For intricate timing
dnl  reasons it does not work simply to let mosmake.dep depend on
dnl  Makefile. Sigh.
AC_CONFIG_COMMANDS(newdeps,[test -f mosmake.dep &&
                            echo $as_me: removing mosmake.dep &&
                            rm -f mosmake.dep])

AC_CONFIG_COMMANDS([set_bpltoolcli_execbit], [chmod a+x bpltoolcli.sh], [])
AC_OUTPUT(Makefile Makefile.chain bpltoolcli.sh)

echo;
echo Configuration complete
echo;

